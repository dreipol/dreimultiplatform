buildscript {
    repositories {
        google()
        jcenter()

    }

    dependencies {
        classpath("com.android.tools.build:gradle:4.0.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id("org.jetbrains.kotlin.multiplatform")
    id("maven-publish")
    id("com.android.library")
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

group = "ch.dreipol"
version = "0.0.3-SNAPSHOT"


kotlin {
    ios {
        binaries {
            framework()
        }
    }

    android {
        publishLibraryVariants("release", "debug")
    }


    sourceSets["commonMain"].dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-common")
    }

    sourceSets["androidMain"].dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib")
    }

    sourceSets["iosMain"].dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib")
    }
    sourceSets["iosMain"].dependsOn(sourceSets["commonMain"])
    sourceSets["iosArm64Main"].dependsOn(sourceSets["iosMain"])
    sourceSets["iosX64Main"].dependsOn(sourceSets["iosMain"])


}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
    }
    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
        }
    }
    buildTypes {
        //This is for MultiplatformSettings
        debug {
            // MPP libraries don't currently get this resolution automatically
            matchingFallbacks = ['release']
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/dreipol/dreimultiplatform")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
