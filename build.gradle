buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }

    }

    dependencies {
        classpath("com.android.tools.build:gradle:4.0.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.0")
        classpath("org.jlleitschuh.gradle:ktlint-gradle:9.3.0")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.multiplatform")
    id("maven-publish")
}
apply plugin: "org.jlleitschuh.gradle.ktlint"


allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

group = "ch.dreipol"


kotlin {
    ios("ios") {
        binaries {
            framework()
        }
    }

    android("android") {
        publishAllLibraryVariants()
    }


    sourceSets["commonMain"].dependencies {
        api("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9-native-mt")
        api("org.reduxkotlin:redux-kotlin-threadsafe:0.5.5")
        api("org.reduxkotlin:reselect:0.5.5")
        api "org.reduxkotlin:redux-kotlin-thunk:0.5.5"
        api("co.touchlab:kermit:0.1.7-1.4.0-rc")
    }

    sourceSets["androidMain"].dependencies {
        implementation("androidx.appcompat:appcompat:1.2.0")
    }

    sourceSets["iosMain"].dependencies {
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
    }
    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
        }
    }
    buildTypes {
        //This is for MultiplatformSettings
        debug {
            // MPP libraries don't currently get this resolution automatically
            matchingFallbacks = ['release']
        }
        release {
            minifyEnabled false
        }
    }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

ktlint {
    ignoreFailures.set(false)
    filter {
        exclude { element -> element.file.path.contains("generated/") }
        exclude { element -> element.file.path.contains("reduxkotlin/") }
    }
}

tasks["build"].dependsOn("ktlintFormat")

publishing {
    repositories {
        maven {
            def user = 'dreipol'
            def repo = 'dreimultiplatform'
            def name = 'ch.dreipol.dreimultiplatform'
            url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=1"
            credentials {
                username = System.getenv("BINTRAY_USER")
                password = System.getenv("BINTRAY_API_KEY")
            }
        }
    }
}
